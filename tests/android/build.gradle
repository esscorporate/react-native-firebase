buildscript {
  repositories {
    google()
    maven {
      url 'https://maven.fabric.io/public'
    }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.0'
    classpath 'com.google.gms:google-services:4.0.1'
    classpath 'com.google.firebase:firebase-plugins:1.1.5'
    classpath 'io.fabric.tools:gradle:1.25.4'
  }
}

subprojects {
  task listAllDependencies(type: DependencyReportTask) {}
}

allprojects {
  task downloadDependencies() {
    description 'Download all dependencies to the Gradle cache'
    doLast {
      configurations.findAll { it.canBeResolved }.files
    }
  }
  repositories {
    mavenLocal()
    google()
    maven {
      url "$rootDir/../node_modules/react-native/android"
    }
    jcenter()
  }
}

subprojects {
  ext {
    compileSdk = 27
    buildTools = "28.0.2"
    minSdk = 18
    targetSdk = 26
  }

  afterEvaluate { project ->
    if (!project.name.equalsIgnoreCase("app")
      && project.hasProperty("android")) {
      android {
        compileSdkVersion compileSdk
        buildToolsVersion buildTools
        defaultConfig {
          minSdkVersion minSdk
          targetSdkVersion targetSdk
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
  distributionUrl = distributionUrl.replace("bin", "all")
}
